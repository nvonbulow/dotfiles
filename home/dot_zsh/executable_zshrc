# You probably want to edit files in ~/.zsh/configs

# Loads zsh config files
_load_configs() {
  _dir="$1"
  # Make sure directory exists
  if [ -d "$_dir" ]; then
    # If preconfigs exist, apply them first
    if [ -d "$_dir/pre" ]; then
      for config in "$_dir"/pre/**/*(N-.); do
        if [ ${config:e} = "zwc" ]; then continue; fi
        source $config
      done
    fi

    for config in "$_dir"/**/*(N-.); do
      case "$config" in
        # Skip preconfigs and postconfigs
        "$_dir"/pre/*)
          :
        ;;
        "$_dir"/post/*)
          :
        ;;
        *)
          if [[ -f $config && ${config:e} != "zwc" ]]; then
            source $config
          fi
        ;;
      esac
    done
      
    # If postconfigs exist, apply them last 
    if [ -d "$_dir/post" ]; then
      for config in "$_dir"/post/**/*(N-.); do
        if [ ${config:e} = "zwc" ]; then continue; fi
        source $config
      done
    fi
  fi
}

_load_configs ~/.zsh/configs

ZIM_HOME=~/.zim
if [[ ! ${ZIM_HOME}/init.zsh -nt ${HOME}/.zimrc ]] ; then
  source ${ZIM_HOME}/zimfw.zsh init
fi
source ~/.zim/init.zsh

# Local config
[[ -f ~/.zshrc.local ]] && source ~/.zshrc.local || true
